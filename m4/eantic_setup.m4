dnl
dnl Copyright (C) 2019 Vincent Delecroix
dnl
dnl This file is part of e-antic
dnl
dnl e-antic is free software: you can redistribute it and/or modify it under
dnl the terms of the GNU Lesser General Public License (LGPL) as published
dnl by the Free Software Foundation; either version 3.0 of the License, or
dnl (at your option) any later version.  See <http://www.gnu.org/licenses/>.
dnl
AC_DEFUN([EANTIC_AC_PKG_EANTIC_SETUP],
[
AC_REQUIRE([AM_INIT_AUTOMAKE])
dnl AC_REQUIRE([AC_CANONICAL_HOST])
dnl
dnl See `Versioning' in the libtool manual for details,
dnl in particular subsection 7.3 `Updating library version information'.
dnl
EANTIC_VERSION_MAJOR=${PACKAGE_VERSION%%.*}
EANTIC_VERSION_MINOR=${PACKAGE_VERSION#*.}
EANTIC_VERSION_MINOR=${EANTIC_VERSION_MINOR%%.*}
EANTIC_VERSION_MICRO=${PACKAGE_VERSION%-*}
EANTIC_VERSION_MICRO=${EANTIC_VERSION_MICRO%~*}
EANTIC_VERSION_MICRO=${EANTIC_VERSION_MICRO#*.}
EANTIC_VERSION_MICRO=${EANTIC_VERSION_MICRO#*.}
EANTIC_VERSION_NANO=0
EANTIC_VERSION_EXTRA="${PACKAGE_VERSION}-"
EANTIC_VERSION_EXTRA=${EANTIC_VERSION_EXTRA#*-}
EANTIC_VERSION_EXTRA=${EANTIC_VERSION_EXTRA%-*}
if test "x${EANTIC_VERSION_EXTRA}" != "x" ;then
	EANTIC_VERSION_EXTRA=\"-${EANTIC_VERSION_EXTRA}\"
else
	EANTIC_VERSION_EXTRA="${PACKAGE_VERSION}~"
	EANTIC_VERSION_EXTRA=${EANTIC_VERSION_EXTRA#*~}
	EANTIC_VERSION_EXTRA=${EANTIC_VERSION_EXTRA%~*}
	if test "x${EANTIC_VERSION_EXTRA}" != "x" ;then
		EANTIC_VERSION_EXTRA=\"~${EANTIC_VERSION_EXTRA}\"
	else
		EANTIC_VERSION_EXTRA=\"\"
	fi
fi
dnl
EANTIC_LT_INFO_CURRENT="$(expr ${EANTIC_VERSION_MAJOR} + ${EANTIC_VERSION_MINOR})"
EANTIC_LT_INFO_REVISION=${EANTIC_VERSION_MICRO}
EANTIC_LT_INFO_AGE=${EANTIC_VERSION_MINOR}
EANTIC_LT_INFO=${EANTIC_LT_INFO_CURRENT}:${EANTIC_LT_INFO_REVISION}:${EANTIC_LT_INFO_AGE}
dnl
AC_SUBST(EANTIC_VERSION_MAJOR)
AC_SUBST(EANTIC_VERSION_MINOR)
AC_SUBST(EANTIC_VERSION_MICRO)
AC_SUBST(EANTIC_VERSION_NANO)
AC_SUBST(EANTIC_VERSION_EXTRA)
dnl AC_SUBST(EANTIC_LT_INFO_CURRENT)
dnl AC_SUBST(EANTIC_LT_INFO_REVISION)
dnl AC_SUBST(EANTIC_LT_INFO_AGE)
AC_SUBST(EANTIC_LT_INFO)
])
dnl eof
